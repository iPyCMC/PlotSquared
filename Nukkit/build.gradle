repositories {
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url = "http://nexus.hc.to/content/repositories/pub_releases" }
    //jitpack.io
    maven { url = "https://jitpack.io" }
    maven { url = "https://repo.opencollab.dev/repository/maven-releases/" }
    maven { url = "https://repo.opencollab.dev/maven-snapshots/" }
    maven { url = "https://repo1.maven.org/maven2/" }
}

dependencies {
    implementation(project(":Core"))
    compile(project(":Core"))
    compile("com.google.guava:guava:21.0")
    compile("com.github.CloudburstMC:Nukkit:master-SNAPSHOT") {
        exclude(module: "natives")
        exclude(module: "fastutil-lite")
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

processResources {
    from("src/main/resources") {
        include "plugin.yml"
        expand(
                name: project.parent.name,
                version: project.parent.version
        )
    }
}

jar.archiveFileName = "plotsquared-nukkitapi-${project.parent.version}.jar"
jar.destinationDirectory = file("../mvn/com/plotsquared/plotsquared-nukkitapi/" + project.parent.version)
task createPom {
    doLast {
        pom {
            project {
                groupId = rootProject.group
                artifactId = "plotsquared-nukkitapi"
                version = rootProject.version
            }
        }.writeTo("../mvn/com/plotsquared/plotsquared-nukkitapi/${project.parent.version}/plotsquared-nukkitapi-${project.parent.version}.pom")
        pom {
            project {
                groupId = rootProject.group
                artifactId = "plotsquared-nukkitapi"
                version = "latest"
            }
        }.writeTo("../mvn/com/plotsquared/plotsquared-nukkitapi/latest/plotsquared-nukkitapi-latest.pom")
         .writeTo("pom.xml")
    }
}

task copyFiles {
    doLast {
        copy {
            from("../mvn/com/plotsquared/plotsquared-nukkitapi/${project.parent.version}/")
            into("../mvn/com/plotsquared/plotsquared-nukkitapi/latest/")
            include("plotsquared-nukkitapi*.jar")
            rename("plotsquared-nukkitapi-${project.parent.version}.jar", "plotsquared-nukkitapi-latest.jar")
        }
    }
}

shadowJar {
    dependencies {
        include(dependency(':Core'))
    }
    archiveFileName = "${parent.name}-${project.name}-${parent.version}.jar"
    destinationDirectory = file "../target"

    relocate("com.google.gson", "com.sk89q.worldedit.internal.gson")
    relocate("org.yaml.snakeyaml", "com.boydti.fawe.yaml")
    relocate("com.google.common", "com.sk89q.worldedit.internal.common")
}

shadowJar.doLast {
    task ->
        ant.checksum file: task.archivePath
}

build.dependsOn(shadowJar)
build.finalizedBy(copyFiles)
copyFiles.dependsOn(createPom)
